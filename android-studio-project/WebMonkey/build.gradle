apply from: '../constants.gradle'
apply plugin: 'com.android.application'

if (file("sign.gradle").exists()) {
  apply from: 'sign.gradle'
}

android {
  compileSdkVersion project.ext.compileSdkVersion
  buildToolsVersion project.ext.buildToolsVersion

  compileOptions {
    sourceCompatibility project.ext.javaVersion
    targetCompatibility project.ext.javaVersion
  }

  defaultConfig {
    minSdkVersion project.ext.minSdkVersion
    targetSdkVersion project.ext.targetSdkVersion

    applicationId "com.github.warren_bank.webmonkey"
    versionName project.ext.releaseVersion
    versionCode project.ext.releaseVersionCode

    archivesBaseName += "-$versionName"
  }

  buildTypes {
    release {
      shrinkResources true
      minifyEnabled   true
      proguardFiles = [
        "proguard-rules.txt",
        getDefaultProguardFile('proguard-android.txt')
      ]


      if (file("sign.gradle").exists()) {
        // Local environment
        signingConfig signingConfigs.release
        // debuggable true

        /* sign.gradle is like:
android {
signingConfigs {
release {
    keyAlias 'aliasxxx'
    keyPassword 'passwordxxx'
    storeFile file('../xxx.jks')
    storePassword 'passwordxxx'
}
}
}
GitHub config is like:
SIGNING_KEY:
`openssl base64 < some_signing_key.jks | tr -d '\n'`
ALIAS:
KEY_STORE_PASSWORD:
KEY_PASSWORD:
*/
      }
    }
    debug {
      debuggable  true
      jniDebuggable true
    }
  }

  lintOptions {
    disable 'MissingTranslation'
    abortOnError true
  }
}

dependencies {
  implementation project(':webview-gm-lib')
  implementation project(':webview-gm-ui')
}
